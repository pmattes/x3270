dnl Copyright (c) 2000-2024 Paul Mattes.
dnl All rights reserved.
dnl 
dnl Redistribution and use in source and binary forms, with or without
dnl modification, are permitted provided that the following conditions
dnl are met:
dnl     * Redistributions of source code must retain the above copyright
dnl       notice, this list of conditions and the following disclaimer.
dnl     * Redistributions in binary form must reproduce the above copyright
dnl       notice, this list of conditions and the following disclaimer in the
dnl       documentation and/or other materials provided with the distribution.
dnl     * Neither the name of Paul Mattes nor his contributors may be used
dnl       to endorse or promote products derived from this software without
dnl       specific prior written permission.
dnl 
dnl THIS SOFTWARE IS PROVIDED BY PAUL MATTES "AS IS" AND ANY EXPRESS
dnl OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
dnl WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
dnl DISCLAIMED. IN NO EVENT SHALL PAUL MATTES BE LIABLE FOR ANY DIRECT,
dnl INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
dnl (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
dnl SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
dnl HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT,
dnl STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING
dnl IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
dnl POSSIBILITY OF SUCH DAMAGE.

dnl Process this file with autoconf to produce a configure script.
AC_INIT([tcl3270],[4.3])
AC_PREREQ([2.69])

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_CC

dnl Figure out what sort of host this is.
AC_CANONICAL_HOST
ansi="-std=c99 -pedantic"
case "$host_os" in
cygwin*)	CPPFLAGS="$CPPFLAGS -D_XOPEN_SOURCE_EXTENDED -D_GNU_SOURCE"
		ansi=""
		;;
solaris2*)	CPPFLAGS="$CPPFLAGS -D__EXTENSIONS__"
		;;
darwin*)	CCOPTIONS="$CCOPTIONS -no-cpp-precomp -Wno-deprecated-declarations"
		;;
linux*)		CPPFLAGS="$CPPFLAGS -D_XOPEN_SOURCE -D_BSD_SOURCE -D_DEFAULT_SOURCE -D_GNU_SOURCE"
		;;
esac
if test "$GCC" = yes
then	# Add common gcc options, plus flags for dependency generation.
    	CCOPTIONS="$CCOPTIONS -Wall -Wsign-compare $ansi -Wno-variadic-macros -Wno-long-long -MMD -MP"
fi
AC_SUBST(host)
AC_SUBST(CCOPTIONS)

dnl Figure out what version of Tcl they've got
AC_CHECK_PROG(tclsh,tclsh,yes,no)
if test "$tclsh" = no
then	AC_MSG_ERROR(Can't find tclsh)
fi
AC_MSG_CHECKING(Tcl version)
tclver=`echo "puts [[set tcl_version]]" | tclsh`
if test -z "$tclver"
then	AC_MSG_ERROR(Can't figure out Tcl version)
fi
AC_MSG_RESULT($tclver)

dnl If Tcl >= 8.6, we need pthreads.
AC_PROG_AWK
pthread=`echo $tclver | $AWK -F . '{ if ($1 > 8 || $2 >= 6) print "1"}'`

dnl Set up tclvr (tclver without the period, for BSD)
tclvr=`echo $tclver | tr -d .`

dnl Checks for header files.
AC_CONFIG_HEADERS([conf.h])
AC_CHECK_HEADERS(util.h)

dnl Check for pthreads.
if test "$pthread"
then	AC_SEARCH_LIBS(pthread_create, pthread)
fi

dnl Check for TCL header files.
for dir in /usr/include/tcl$tclver /usr/include/tcl$tclvr /usr/local/include /usr/local/include/tcl$tclver /usr/local/include/tcl$tclvr
do	if test -d $dir
    	then	CPPFLAGS="$CPPFLAGS -I$dir"
	fi
done
AC_CHECK_HEADERS(tcl.h, ,[AC_MSG_ERROR(Cannot find tcl header file)])

dnl Checks for orindary library functions.
AC_CHECK_FUNCS(vasprintf)
AC_FUNC_FSEEKO

dnl Check for libraries.
dnl Note that the order here is important.  The last libraries should appear
dnl first, so that objects in them can be used by subsequent libraries.
LDFLAGS="$LDFLAGS -lm"
AC_SEARCH_LIBS(gethostbyname, nsl)
AC_SEARCH_LIBS(socket, socket)

if test -d /usr/local/lib
then	LDFLAGS="$LDFLAGS -L/usr/local/lib"
fi
tcl_failed=0
AC_CHECK_LIB(tcl$tclver, Tcl_Init, , [tcl_failed=1])
if test $tcl_failed = 1
then	AC_CHECK_LIB(tcl$tclvr, Tcl_Init, , [AC_MSG_ERROR(Cannot find TCL library)])
fi

dnl Set up the configuration directory.
LIBX3270DIR='${sysconfdir}/x3270'
AC_SUBST(LIBX3270DIR)

dnl Check for unwanted parts.
AC_ARG_ENABLE(dbcs,[  --disable-dbcs          leave out DBCS support])
case "$enable_dbcs" in
no)	;;
*)	AC_DEFINE(X3270_DBCS,1)
	;;
esac
AC_ARG_ENABLE(local_process,[  --disable-local-process leave out local process support])
case "$enable_local_process" in
""|yes)	AC_DEFINE(X3270_LOCAL_PROCESS,1)
	;;
esac

dnl Generate the Makefile.
AC_CONFIG_FILES(Makefile Makefile.obj)
AC_OUTPUT
